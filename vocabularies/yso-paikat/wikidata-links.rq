PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX yso-meta: <http://www.yso.fi/onto/yso-meta/>

CONSTRUCT {
    ?ysoc skos:closeMatch ?item .
    ?ysoc skos:prefLabel ?itemLabel .
    ?item wdt:P625 ?point .
    ?item yso-meta:wikidataPlaceType ?type .
    ?type skos:prefLabel ?typeLabel .
}
WHERE
{
    ?item wdt:P2347 ?ysoid .
    FILTER(STRDT(?ysoid, xsd:integer) > 50000)
    BIND(IRI(CONCAT('http://www.yso.fi/onto/yso/p', ?ysoid)) AS ?ysoc)
    OPTIONAL {
      ?item rdfs:label ?itemLabel .
      FILTER(LANG(?itemLabel)='en')
    }
    OPTIONAL {
      ?item wdt:P625 ?point .
    }
    OPTIONAL {
      ?item wdt:P31 ?type .
      ?type rdfs:label ?typeLabel .
      FILTER(LANG(?typeLabel)='en' || LANG(?typeLabel)='fi' || LANG(?typeLabel)='sv')
      VALUES ?type {wd:Q6256 wd:Q35657 wd:Q5107 wd:Q9430 wd:Q515}
    }
}

