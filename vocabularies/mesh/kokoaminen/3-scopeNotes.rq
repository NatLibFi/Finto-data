PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX meshv: <http://id.nlm.nih.gov/mesh/vocab#>
PREFIX mesh: <http://id.nlm.nih.gov/mesh/>
PREFIX mesh2015: <http://id.nlm.nih.gov/mesh/2015/>
PREFIX mesh2016: <http://id.nlm.nih.gov/mesh/2016/>
PREFIX mesh2017: <http://id.nlm.nih.gov/mesh/2017/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

CONSTRUCT {
  ?desc skos:scopeNote ?snote . 
  ?desc skos:historyNote ?hnote . 
  ?desc skos:note ?anno . 
}
WHERE { 
  ?desc a ?type . 
  ?desc meshv:preferredConcept ?conc .
  OPTIONAL { ?desc meshv:historyNote ?hnote . }
  OPTIONAL { ?desc meshv:annotation ?anno . }
  OPTIONAL { ?conc meshv:scopeNote ?snote . }
  VALUES (?type) { (meshv:TopicalDescriptor) (meshv:GeographicalDescriptor) (meshv:PublicationType) }
}
