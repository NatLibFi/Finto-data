PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ysa: <http://www.yso.fi/onto/ysa/>
PREFIX allars: <http://www.yso.fi/onto/allars/>
PREFIX yso: <http://www.yso.fi/onto/yso/>
PREFIX ysa-meta: <http://www.yso.fi/onto/ysa-meta/>
PREFIX yso-meta: <http://www.yso.fi/onto/yso-meta/2007-03-02/>
PREFIX yso-update: <http://www.yso.fi/onto/yso-update/>
PREFIX dc: <http://purl.org/dc/elements/1.1/> 
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX om: <http://www.yso.fi/onto/yso-peilaus/2007-03-02/>
PREFIX dsv: <http://purl.org/iso25964/DataSet/Versioning#>
PREFIX sh: <http://purl.org/skos-history/>
PREFIX sd: <http://www.w3.org/ns/sparql-service-description#>

CONSTRUCT {
  ?ysoc om:definedConcept ?ac .
  ?ysoc skos:prefLabel ?pref .
  ?ysoc yso-meta:oldLabel ?alt .
  ?ysoc skos:note ?note .
  ?ysoc yso-meta:ysaSource ?source .
  ?ysoc rdfs:subClassOf yso-update:uudetSv .
}
WHERE {
  {
    SELECT ?ysoc ?ysac
    WHERE {
      ?ysoc om:definedConcept ?ysac .
      FILTER(STRSTARTS(STR(?ysac), STR(ysa:)))
      FILTER NOT EXISTS {
        ?ysoc om:definedConcept ?ac .
        FILTER(STRSTARTS(STR(?ac), STR(allars:)))
      }
    }
  }
  SERVICE <http://sparql.dev.finto.fi/skos-history/sparql> {
    GRAPH <http://www.yso.fi/onto/ysa/version> {
      # find the graph URI of the latest available version of YSA
      ?ysaversionset dsv:currentVersionRecord/sh:usingNamedGraph/sd:name ?ysaNewVersionGraph .
    }
    GRAPH <http://www.yso.fi/onto/allars/version> {
      # find the graph URI of the latest available version of All√§rs
      ?allarsversionset dsv:currentVersionRecord/sh:usingNamedGraph/sd:name ?allarsNewVersionGraph .
    }
    GRAPH ?ysaNewVersionGraph {
      ?ysac skos:exactMatch ?ac .
      FILTER(STRSTARTS(STR(?ac), STR(allars:)))
    }
    GRAPH ?allarsNewVersionGraph {
      ?ac skos:prefLabel ?pref .
      OPTIONAL { ?ac skos:altLabel ?alt }
      OPTIONAL { ?ac skos:scopeNote|skos:note ?note }
      OPTIONAL { ?ac dc:source ?source }
    }
  }
}
