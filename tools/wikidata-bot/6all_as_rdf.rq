PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX ps: <http://www.wikidata.org/prop/statement/>
PREFIX pq: <http://www.wikidata.org/prop/qualifier/>
PREFIX pr: <http://www.wikidata.org/prop/reference/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

CONSTRUCT {
  # YSOon liittyvät
  ?item p:P2347 ?statement .
  # Korjaa: tätä pitää muuttaa niin, että ?yso:lle lisätään jo tässä vaiheessa oikea yso-uri p-kirjaimen kanssa
  # koska se helpottaa myöhempiä vaiheita, kun sitä ei tarvitse erikseen lisäillä.
  ?statement ps:P2347 ?yso .
  ?statement wikibase:rank ?rank .
  
  # Qualifierit
  ?statement pq:P1810 ?subjectNamedAs .

  # Referenssit ja "ketjut" eli bnode-hommelit
  ?statement prov:wasDerivedFrom ?reference .
  ?reference pr:P248 ?statedIn .
  ?reference pr:P813 ?retrieved .
  ?reference rdfs:label ?referenceLabel .

  # Itse referenssi subjektina ja objektina
  ?reference pr:P248 ?source .
  ?source rdfs:label ?sourceLabel .

} WHERE {
  # YSO ID
  ?item p:P2347 ?statement .
  ?statement ps:P2347 ?yso .
  ?statement wikibase:rank ?rank .

  # Qualifierit optional tyyliin
  OPTIONAL { ?statement pq:P1810 ?subjectNamedAs . }

  # Viittauket ja "viittausketjut", bnodet
  OPTIONAL {
    ?statement prov:wasDerivedFrom ?reference .
    ?reference pr:P248 ?statedIn .
    ?reference pr:P813 ?retrieved .
    OPTIONAL { ?reference rdfs:label ?referenceLabel . }

    # Lisätietoja viittauksia seuraamalla
    OPTIONAL {
      ?reference pr:P248 ?source .
      ?source rdfs:label ?sourceLabel .
    }
  }
}
LIMIT 10000 # Väliaikaisesti debuggauksen ajan
